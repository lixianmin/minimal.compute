#pragma kernel CSMain

struct Boid
{
    float3 position;
    float3 direction;
};

RWStructuredBuffer<Boid> boidsBuffer;

float time;
float deltaTime;
float rotationSpeed;
float boidSpeed;
float boidSpeedVariation;
float3 flockPosition;
float neighbourDistance;
int boidsCount;

[numthreads(64,1,1)]
void CSMain(int3 id : SV_DispatchThreadID)
{
    if (id.x >= boidsCount)
    {
        return;
    }
    
    Boid boid = boidsBuffer[id.x];

    float3 separation = 0;
    float3 alignment = 0;
    float3 cohesion = flockPosition;

    uint nearbyCount = 1; // Add self that is ignored in loop

    for (int i = 0; i < boidsCount; i++)
    {
        if (i != int(id.x))
        {
            const Boid temp_boid = boidsBuffer[i];
            const float3 offset = boid.position - temp_boid.position;
            const float dist = max(length(offset), 0.00001);
            if (dist < neighbourDistance)
            {
                separation += offset * (1.0 / dist - 1.0 / neighbourDistance);
                alignment += temp_boid.direction;
                cohesion += temp_boid.position;
                nearbyCount++;
            }
        }
    }

    const float average = 1.0 / nearbyCount;
    alignment *= average;
    cohesion *= average;
    cohesion = normalize(cohesion - boid.position);

    const float3 direction = alignment + separation + cohesion;

    const float prop = 0.94;
    boid.direction = lerp(direction, normalize(boid.direction), prop);
    boid.position += boid.direction * boidSpeed * deltaTime;

    boidsBuffer[id.x] = boid;
}
